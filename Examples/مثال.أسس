اشمل "مـتم/طـرفية"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Faiss"، "فـيس.أسس")؛
استخدم مـتم؛
استخدم فـيس؛

// بيانات الإدخال
عرف _ب_: صحيح = 4؛
عرف سب: مـصفوفة[عـائم]({
    1.0،  2.0،  3.0،  4.0،
    2.0،  3.0،  4.0،  5.0،
    10.0، 11.0، 12.0، 13.0،
    11.0، 12.0، 13.0، 14.0،
    5.0،  6.0،  7.0،  8.0
})؛
عرف سس: مـصفوفة[عـائم]({
    1.5،  2.5،  3.5،  4.5،
    10.5، 11.5، 12.5، 13.5
})؛

// تحضير الفهرس
عرف الفهرس: سند[فـهرس]؛
إذا فـهرس.أنشئ(الفهرس، _ب_، "Flat"، نـوع_قياس._نتاج_داخلي_) == 0 {
    طـرفية.اطبع("فـهرس.أنشئ: نجح\ج")؛
} وإلا {
    طـرفية.اطبع("فـهرس.أنشئ: خطأ\ج")؛
}
الفهرس.أضف(سب.هات_الطول() / _ب_، سب.صوان)؛

// تنفيذ البحث
عرف الوسوم: مصفوفة[صحيح[64]، 6]؛
عرف المسافات: مصفوفة[عـائم، 6]؛
الفهرس.ابحث(سس.هات_الطول() / _ب_، سس.صوان، 3، المسافات، الوسوم)؛

// طباعة النتائج
عرف ت: صحيح؛
عرف ج: صحيح؛
لكل ت = 0، ت < سس.هات_الطول() / _ب_، ت++ {
    طـرفية.اطبع("الاستعلام %i:\n"، ت)؛
    لكل ج = 0، ج < 3، ج++ {
        طـرفية.اطبع("  الجار ")؛
        طـرفية.اطبع(ج)؛
        طـرفية.اطبع("، رقم= ")؛
        طـرفية.اطبع(الوسوم(ت * 3 + ج))؛
        طـرفية.اطبع("، مسافة= ")؛
        طـرفية.اطبع(المسافات(ت * 3 + ج))؛
        طـرفية.اطبع("\n")؛
    }
}

فـهرس.حرر(الفهرس)؛
