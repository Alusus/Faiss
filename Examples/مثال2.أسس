اشمل "مـتم/طـرفية"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Faiss"، "فـيس.أسس")؛
استخدم مـتم؛
استخدم فـيس؛

عرف بـ: صحيح = 8؛
عرف عب: صحيح[64] = 80؛
عرف عس: صحيح[64] = 2؛
عرف ك: صحيح[64] = 3؛

// تحضير البيانات
عرف سب: مـصفوفة[عـائم]؛
عرف ت: صحيح[64] = 0؛
لكل ت = 0، ت < عب * بـ، ت++ {
    سب.أضف((ت % بـ) + 1)؛
}
عرف سس: مـصفوفة[عـائم]؛
لكل ت = 0، ت < عس * بـ، ت++ {
    سس.أضف((ت % بـ) + 1.5)؛
}

// تحضير الفهرس
عرف الفهرس: سند[فـهرس_ملف_معكوس]؛
إذا فـهرس.أنشئ(الفهرس، بـ، "IVF1,Flat"، نـوع_قياس._ل2_) == 0 {
    طـرفية.اطبع("فـهرس.أنشئ: نجح\n")؛
} وإلا {
    طـرفية.اطبع("فـهرس.أنشئ: خطأ\n")؛
}
الفهرس.درب(عب، سب.صوان)؛
الفهرس.أضف(عب، سب.صوان)؛

عرف فض: سند[فـضاء_وسيط]؛
فـضاء_وسيط.أنشئ(فض)؛
فض.حدد_وسيط_فهرس(الفهرس، "nprobe"، 2.0)؛
فـضاء_وسيط.حرر(فض)؛

// تنفيذ البحث
عرف الوسوم: مصفوفة[صحيح[64]، 6]؛
عرف المسافات: مصفوفة[عـائم، 6]؛
الفهرس.ابحث(عس، سس.صوان، 3، المسافات، الوسوم)؛

// طباعة النتائج
عرف ج: صحيح = 0؛
لكل ت = 0، ت < عس، ت++ {
    طـرفية.اطبع("الاستعلام %i:\n"، ت)؛
    لكل ج = 0، ج < 3، ج++ {
        طـرفية.اطبع("  الجار ")؛
        طـرفية.اطبع(ج)؛
        طـرفية.اطبع("، رقم= ")؛
        طـرفية.اطبع(الوسوم(ت * 3 + ج))؛
        طـرفية.اطبع("، مسافة= ")؛
        طـرفية.اطبع(المسافات(ت * 3 + ج))؛
        طـرفية.اطبع("\n")؛
    }
}

فـهرس.حرر(الفهرس)؛
